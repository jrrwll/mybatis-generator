allprojects {
    repositories {
        // mavenLocal()
        mavenCentral()
        maven { url 'https://central.sonatype.com/repository/maven-snapshots/' }
    }

    apply plugin: 'java-library'
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
    javadoc.options.encoding = 'UTF-8'

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    version = VERSION
    group = GROUP_ID

    dependencies {
        compileOnly libs.lombok
        annotationProcessor libs.lombok
        testCompileOnly libs.lombok
        testAnnotationProcessor libs.lombok
        testImplementation libs.jupiter.api
        testRuntimeOnly libs.jupiter.engine
    }

    test {
        useJUnitPlatform()
    }

    tasks.register('printVersion') {
        doLast {
            print version
        }
    }

    if (file("${projectDir}/gradle.properties").exists()) {
        apply from: new File(rootDir, 'gradle/publish.gradle')
    }
    apply from: new File(rootDir, 'gradle/tasks-jar.gradle')
}

dependencies {
    implementation libs.slf4j.api
    implementation libs.slf4j.simple
    implementation libs.dreamcat.common
    implementation libs.bundles.jackson
    implementation libs.jsqlparser

    testImplementation 'org.mybatis:mybatis:3.5.10'
    // testRuntimeOnly 'mysql:mysql-connector-java:8.0.29'
    // https://mvnrepository.com/artifact/com.h2database/h2
    testRuntimeOnly 'com.h2database:h2:2.3.232'
}

processTestResources {
    from('src/test/java') {
        include '**/*.xml'
    }
}
